import org.reflections.Reflections
import org.reflections.scanners.TypeAnnotationsScanner
import org.reflections.util.ConfigurationBuilder

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.reflections:reflections:0.9.5'
    }
}

task collectPlugins(dependsOn: ['classes'] ){
    description = "Collect JIP plugins and write store to resources"
    ext.targetFile = new File("${project.sourceSets.main.output.resourcesDir.absolutePath}/META-INF/reflections/${project.name}-reflections.xml")
    ext.classesDir = new File("${project.sourceSets.main.output.classesDir.absolutePath}")

    outputs.file targetFile
    inputs.dir classesDir

    doLast{
        //outputs.file file(bp)
        Reflections reflections = new Reflections(new ConfigurationBuilder()
                .setScanners(new TypeAnnotationsScanner())
                .addUrls(classesDir.toURI().toURL())
        );
        reflections.save(targetFile.absolutePath)
    }
}

tasks.jar.dependsOn tasks.collectPlugins